plugins {
    id 'org.jetbrains.intellij' version "0.4.18"
}

apply plugin: 'java'
apply plugin: 'idea'

patchPluginXml {
    changeNotes """
        <ul>
            <li>#239 - Experimental: start of ESY support work</li>
            <li>#249 - Too many warnings in log (bsconfig not found)</li>
            <li>#241 - Fix bug that prevent IDEA to keep correctly the source of SDK</li>
            <li>#236 - Cannot find a module definition if namespace is used</li>
            <li>Find real bsc/bsb binaries if a symlink is found</li>
            <li>Fix gutter icons for navigation in OCaml files</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

intellij {
    pluginName 'reasonml-plugin-idea'
    type 'IU'
    version iuVersion
    updateSinceUntilBuild = false
    plugins = ['java',
               'JavaScriptLanguage',
               'com.jetbrains.hackathon.indices.viewer:1.3',
               'PsiViewer:' + psiVersion
    ]
}

allprojects {
    def jdkVersion = 1.8

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    idea {
        project {
            jdkName = jdkVersion
            languageLevel = jdkVersion
        }
        module {
            generatedSourceDirs += file('gen')
        }
    }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

/*
git log --oneline | Select-String -Pattern "(191|192|193) -" | %{$_.Line.Split(" ")[0]}
*/
