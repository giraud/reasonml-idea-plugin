plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.3'  // https://github.com/JetBrains/gradle-intellij-plugin
}

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.intellij'

    group 'com.reason'
    version = pluginVersion + '-' + platformVersion
    sourceCompatibility = '11.0.9'

    compileJava.options.encoding = 'UTF-8'

    dependencies {
        testImplementation 'junit:junit:4.12'
    }

    sourceSets {
        main {
            java.srcDirs 'src'
            resources.srcDirs 'resources'
        }
        test {
            java.srcDirs 'tests'
        }
    }

    intellij {
        pluginName = pluginName
        type = platformType
        version = platformVersion
        plugins = [
                'java',
                'JavaScriptLanguage',
                'com.jetbrains.hackathon.indices.viewer:' + indicesVersion,
                'PsiViewer:' + psiVersion
        ]

        patchPluginXml {
            sinceBuild = pluginSinceBuild
            untilBuild = pluginUntilBuild
            changeNotes """
                <ul>
                    <li>#333 - CLion crash, remove dependency to java only class</li>
                    <li>#328 - Update Rescript parser to latest syntax</li>
                    <li>#326 - Structure panel shows "mutable" for mutable record fields rather than the field name</li>
                    <li>#318 - Rescript integration</li>
                    <li>#157 - Update parameter info handler, only show known types</li>
                </ul>
                <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>
                <p/>
                <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
            """
        }
    }

    runIde {
        systemProperty 'idea.is.internal', true
    }

    verifyPlugin {
        pluginDirectory 'resources'
    }
}

project(':jps-plugin') {
    jar.archiveFileName = "jps-plugin.jar"
    patchPluginXml.enabled = false
}

dependencies {
    implementation project(':jps-plugin')
}
