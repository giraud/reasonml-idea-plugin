plugins {
    id 'org.jetbrains.intellij' version "0.4.18"
}

def pluginVersion = '0.87.1'
def ideaVersion = '2020.1-EAP1'
version = pluginVersion + '-' + ideaVersion

patchPluginXml {
    changeNotes """
        <ul>
            <li>214 - Infinite indexing</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        pluginName 'reasonml-plugin-idea'
        type 'IU'
        version '201.6668.13-EAP-SNAPSHOT' // https://www.jetbrains.com/intellij-repository/releases
        updateSinceUntilBuild = false
        plugins = ['java',
                   'JavaScriptLanguage',
                   'com.jetbrains.hackathon.indices.viewer:1.3',
                   'PsiViewer:201.6251.22-EAP-SNAPSHOT.3' // https://plugins.jetbrains.com/plugin/227-psiviewer/versions
        ]
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED', '-Dcompiler.process.debug.port=5005', '-Xmx1024m']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

verifyPlugin {
    pluginDirectory "resources"
}

/*
git log --oneline | Select-String -Pattern "(191|192|193) -" | %{$_.Line.Split(" ")[0]}
*/
