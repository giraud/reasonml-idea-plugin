plugins {
    id 'org.jetbrains.intellij' version "0.4.21"
}

apply plugin: 'idea'

def jdkVersion = 1.8

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
    }
}

patchPluginXml {
    changeNotes """
        <ul>
            <li>a new unified language settings</li>
            <li>Support GoTo/Implementation(s)</li>
            <li>Incompatible with 2020.2 IDEs</li>
            <li>Regression: Structure panel should group methods under the class name</li>
            <li>Reworked parsers (try to remove some complexity) - rescript included</li>
            <li>.res files are automatically compiled when saved, like .re</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    group 'com.reason'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.intellij'

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    repositories {
        mavenCentral()
    }

    dependencies {
        def powerMockVersion = '2.0.2'
        testCompile 'org.mockito:mockito-core:3.3.3'
        testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
        testCompile "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

project(':idea-plugin') {
    dependencies {
        compile project(':jps-plugin')
    }
}
