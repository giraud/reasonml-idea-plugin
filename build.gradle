plugins {
    id 'org.jetbrains.intellij' version "0.4.18"
}

def pluginVersion = '0.89.0'
version = pluginVersion + '-' + ideaVersion

patchPluginXml {
    changeNotes """
        <ul>
            <li>#239 - Experimental: start of ESY support work</li>
            <li>#249 - Too many warnings in log (bsconfig not found)</li>
            <li>#241 - Fix bug that prevent IDEA to keep correctly the source of SDK</li>
            <li>#236 - Cannot find a module definition if namespace is used</li>
            <li>Find real bsc/bsb binaries if a symlink is found</li>
            <li>Fix gutter icons for navigation in OCaml files</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        pluginName 'reasonml-plugin-idea'
        type 'IU'
        version iuVersion
        updateSinceUntilBuild = false
        plugins = ['java',
                   'JavaScriptLanguage',
                   'com.jetbrains.hackathon.indices.viewer:1.3',
                   'PsiViewer:' + psiVersion
        ]
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED', '-Dcompiler.process.debug.port=5005', '-Xmx1024m']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
    def powerMockVersion = "2.0.2"
    testCompile 'org.mockito:mockito-core:3.3.3'
    testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testCompile "org.powermock:powermock-api-mockito2:${powerMockVersion}"
}

apply plugin: 'idea'
idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

verifyPlugin {
    pluginDirectory "resources"
}

/*
git log --oneline | Select-String -Pattern "(191|192|193) -" | %{$_.Line.Split(" ")[0]}
*/
